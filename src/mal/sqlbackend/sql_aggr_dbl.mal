# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0.  If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Copyright 1997 - July 2008 CWI, August 2008 - 2018 MonetDB B.V.

function aggr.covar(e1:bat[:dbl], e2:bat[:dbl]):dbl;
	e0:bat[:dbl] := batcalc.*(e1,e2);
	s0:dbl := aggr.sum(e0);
	s1:dbl := aggr.sum(e1);
	s2:dbl := aggr.sum(e2);
	v2:dbl := calc.*(s1,s2);
	c := aggr.count(e1);
	n:dbl := calc.dbl(c);
	v3:dbl := calc./(v2,n);
	v1:dbl := calc.-(s0,v3);
	v:dbl := calc./(v1,n);
	return v;
end aggr.covar;

function aggr.corr(e1:bat[:dbl], e2:bat[:dbl]):dbl;
	cv:dbl := aggr.covar(e1,e2);
	sd1 := aggr.stdev(e1);
	sd2 := aggr.stdev(e2);
	sd := calc.*(sd1,sd2);
	r := calc./(cv,sd);
	res := calc.dbl(r);
	return res;
end aggr.corr;

function aggr.subcovar(e1:bat[:dbl], e2:bat[:dbl], g:bat[:oid], e:bat[:any_2], f:bit):bat[:dbl];
	e0:bat[:dbl] := batcalc.*(e1,e2);
	s0:bat[:dbl] := aggr.subsum(e0,g,e,f,true);
	s1:bat[:dbl] := aggr.subsum(e1,g,e,f,true);
	s2:bat[:dbl] := aggr.subsum(e2,g,e,f,true);
	v2:bat[:dbl] := batcalc.*(s1,s2);
	c := aggr.subcount(e1,g,e,f);
	n:bat[:dbl] := batcalc.dbl(c);
	v3:bat[:dbl] := batcalc./(v2,n);
	v1:bat[:dbl] := batcalc.-(s0,v3);
	v:bat[:dbl] := batcalc./(v1,n);
	return v;
end aggr.subcovar;

function aggr.subcorr(e1:bat[:dbl], e2:bat[:dbl], g:bat[:oid],e:bat[:any_2], f:bit):bat[:dbl];
	cv:bat[:dbl] := aggr.subcovar(e1,e2,g,e,f);
	sd1 := aggr.substdevp(e1,g,e,f,true);
	sd2 := aggr.substdevp(e2,g,e,f,true);
	sd := batcalc.*(sd1,sd2);
	sdn := batcalc.==(sd,0:dbl);
	sds := batcalc.ifthenelse(sdn,1:dbl,sd);
	r := batcalc./(cv,sds);
	res := batcalc.dbl(r);
	return res;
end aggr.subcorr;
